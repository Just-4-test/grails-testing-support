plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
}

asciidoctor {
    resources {
        from('src/docs/images')
        into "./images"
    }

    attributes 'experimental'          : 'true',
               'compat-mode'           : 'true',
               'toc'                   : 'left',
               'icons'                 : 'font',
               'testingSupportVersion' : "${rootProject.allprojects.find { it.name == 'grails-testing-support'}.version}",
               'sourcedir'             : "${rootProject.allprojects.find { it.name == 'grails-testing-support'}.projectDir}/src/main/groovy",
               'mainprojectdir'        : "${rootProject.allprojects.find { it.name == 'grails-testing-support'}.projectDir}",
               'demoAppDir'            : "${rootProject.allprojects.find { it.name == 'demo'}.projectDir}"
}

configurations {
  documentation
}

dependencies {
    documentation project(':grails-testing-support')
    documentation project(':grails-web-testing-support')
    documentation 'org.codehaus.groovy:groovy-all:2.4.7'
}



task apidocs(type: Groovydoc) {
    def allProjects = rootProject.allprojects.findAll { project ->
      ['grails-testing-support', 'grails-web-testing-support', 'grails-gorm-testing-support'].contains(project.name)
    }
    source allProjects.collect { project ->
      project.files('src/main/groovy')
    }

    destinationDir = new File(buildDir, 'docs/api')
    // Might need a classpath
    docTitle = "Grails Testing Support ${rootProject.allprojects.find { it.name == 'grails-testing-support'}.version}"
    
    classpath = configurations.documentation
    groovyClasspath = configurations.documentation
}

task docs(type:Copy, dependsOn:[apidocs, asciidoctor])  {
    from "$buildDir/asciidoc/html5"
    into "$buildDir/docs"
}